buildscript {
	repositories {
		mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:3.3.2"
	}
}

ext."signing.keyId" = project.hasProperty("signing.keyId") ? project.getProperty('signing.keyId') : System.getenv('SIGNING_KEY')
ext."signing.password" = project.hasProperty("signing.password") ? project.getProperty('signing.password') : System.getenv('SIGNING_PASSPHRASE')
ext."signing.secretKeyRingFile" = project.hasProperty("signing.secretKeyRingFile") ? project.getProperty('signing.secretKeyRingFile') : ("${System.properties['user.home']}${File.separator}.gnupg${File.separator}secring.gpg")
ext.isReleaseVersion = !projectVersion.endsWith("SNAPSHOT")

version project.projectVersion
group "org.graceframework.plugins"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "java-library"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
apply plugin: "io.github.gradle-nexus.publish-plugin"
apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: "org.asciidoctor.jvm.convert"

repositories {
	mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" }
}

configurations {
    documentation
}

dependencies {
    api "org.liquibase:liquibase-core:$liquibaseVersion"
    api("org.liquibase.ext:liquibase-hibernate5:$liquibaseHibernateVersion") {
        exclude group: 'org.hibernate', module: 'hibernate-core'
        exclude group: 'org.hibernate', module: 'hibernate-entitymanager'
        exclude group: 'org.hibernate', module: 'hibernate-envers'
        exclude group: 'org.liquibase', module: 'liquibase-core'
        exclude group: 'org.liquibase', module: 'liquibase-test-harness'
        exclude group: 'com.h2database', module: 'h2'
    }
    api("org.graceframework:grace-shell:$graceVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    api("org.codehaus.groovy:groovy-sql:$groovyVersion")
    compileOnly "org.hibernate:hibernate-core:5.6.15.Final"

    compileOnly 'org.springframework.boot:spring-boot-starter-logging'
    compileOnly "org.springframework.boot:spring-boot-starter-actuator"
    compileOnly "org.springframework.boot:spring-boot-autoconfigure"
    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"

    compileOnly "org.graceframework:grace-boot:$graceVersion"
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly "org.graceframework.plugins:hibernate5:$graceHibernatePluginVersion"

    testImplementation "org.springframework.boot:spring-boot-starter-tomcat"
    testImplementation "org.hibernate:hibernate-core:5.6.15.Final"
    testImplementation "org.grails.plugins:hibernate5"

    testImplementation "org.graceframework:grace-test-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation 'com.h2database:h2:1.4.200'

    documentation "org.fusesource.jansi:jansi:1.18"
	documentation "com.github.javaparser:javaparser-core:3.15.14"
    documentation "org.codehaus.groovy:groovy:$groovyVersion"
    documentation "org.codehaus.groovy:groovy-ant:$groovyVersion"
    documentation "org.codehaus.groovy:groovy-templates:$groovyVersion"
    documentation "org.codehaus.groovy:groovy-xml:$groovyVersion"
}

bootJar.enabled = false

sourceCompatibility = 11
targetCompatibility = 11

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    enabled = true
    archiveClassifier.set('')
}

tasks.withType(Groovydoc) {
    configure {
        docTitle = "Grace Database Migration Plugin ${version}"
        source = project.files('src/main/groovy')
        destinationDir = new File(buildDir, 'docs/api')
        classpath = configurations.documentation
        groovyClasspath = configurations.documentation
    }
}

task docs(type:Copy, group: 'documentation')  {
    dependsOn(groovydoc, asciidoctor)
    from "$buildDir/asciidoc"
    into "$buildDir/docs"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            from components.java

            pom {
                name = "Grace Database Migration Plugin"
                description = "Grace Database Migration"
                url = 'https://github.com/graceframework/grace-database-migration'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'rainboyan'
                        name = 'Michael Yan'
                        email = 'rain@rainboyan.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/graceframework/grace-database-migration.git'
                    developerConnection = 'scm:git:ssh://github.com:graceframework/grace-database-migration.git'
                    url = 'https://github.com/graceframework/grace-database-migration/tree/5.0.x'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
            def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
            def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileId") ? project.sonatypeOssStagingProfileId : ''
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = ossUser
            password = ossPass
            stagingProfileId = ossStagingProfileId
        }
    }
}

afterEvaluate {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.maven
    }
}
